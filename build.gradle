plugins {
	id 'java'
	id 'jacoco'
	id "org.sonarqube" version "2.8"
	id 'org.springframework.boot' version '2.2.6.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id "org.liquibase.gradle" version "2.0.2"
}

group = 'com.speech4j'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
	mavenCentral()
}

dependencyManagement {
	imports {
		mavenBom 'org.junit:junit-bom:5.6.0'
		mavenBom 'org.testcontainers:testcontainers-bom:1.13.0'
	}
}

diff.dependsOn assemble
diffChangeLog.dependsOn compileJava

diffChangeLog {
	jvmArgs(['--enable-preview'])
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	runtimeOnly 'org.postgresql:postgresql'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.projectlombok:lombok'
	annotationProcessor "org.projectlombok:lombok"
	implementation 'org.springframework.security:spring-security-crypto'
	implementation 'org.springdoc:springdoc-openapi-ui:1.3.0'
	implementation 'org.liquibase:liquibase-core:3.8.9'

	testImplementation 'org.junit.jupiter:junit-jupiter-api'
	testImplementation 'org.junit.jupiter:junit-jupiter-params'
	testImplementation 'org.testcontainers:testcontainers'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

	liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:3.8'
	liquibaseRuntime sourceSets.main.runtimeClasspath
}

test {
	useJUnitPlatform()
}

sonarqube {
	properties {
		property "sonar.projectKey", "speech4j_tenant-service"
		property "sonar.sourceEncoding", "UTF-8"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

jacocoTestReport {
	reports {
		xml.enabled true
		xml.destination file("${buildDir}/reports/jacoco/text.xml")
		html.enabled false
		csv.enabled false
	}
}

project.tasks["sonarqube"].dependsOn "jacocoTestReport"


//===Liquibase configurations===
Properties properties = new Properties()

Properties metadata = new Properties()
metadata.load(new FileInputStream("src/main/resources/liquibase-metadata.properties"))
//Setting a default schema
properties = metadata;

Properties tenant = new Properties()
tenant.load(new FileInputStream("src/main/resources/liquibase-tenant.properties"))

if(project.hasProperty('schemaType')){
	def requiredSchemaType = 'tenant'
	def inputSchemaType = project.property('schemaType')

	//Checking of an input schema type
	properties = requiredSchemaType.equals(inputSchemaType) ? tenant : properties
}

liquibase {
	activities {
		main {
			changeLogFile properties.getProperty('changeLogFile')
			referenceUrl properties.getProperty('referenceUrl')  //hibernate entities
			url properties.getProperty('url')                    //current db schema
			username properties.getProperty('username')
			password properties.getProperty('password')
			referenceDriver properties.getProperty('referenceDriver')
			excludeObjects 'table:content_contents,content_tags,content_content_tag'
		}
	}
}

tasks.withType(JavaCompile).each {
	it.options.compilerArgs.add('--enable-preview')
}

test {
	jvmArgs(['--enable-preview'])
}